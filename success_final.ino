// í¬ì»¤ ê²½ë§ˆ â€“ Matrix LED + ë²„íŠ¼ + ì‹œë¦¬ì–¼ ëª¨ë‹ˆí„° + ë¶€ì €
#include <Arduino.h>

// === í•€ ë§¤í•‘ ===
const int matrixSourcePins[8] = {30,31,32,33,34,35,36,37};
const int matrixSinkPins[8]   = {22,23,24,25,26,27,28,29};
const int buttonPin = A15;      // í’€ì—… ë²„íŠ¼
const int buzzerPin = 14;       // ë¶€ì € + â†’ D12, - â†’ GND

// === ë””ë°”ìš´ìŠ¤ ===
const unsigned long debounceDelay = 200;
unsigned long lastDebounceTime = 0;
bool prevButtonState = HIGH;

// === ê²Œì„ íŒŒë¼ë¯¸í„° ===
const int horseCount = 4;
const int trackLength = 7;
bool gameOver = false;

// ì¹´ë“œ ë±
enum Suit {DIAMOND, HEART, SPADE, CLOVER};
enum CardType {NORMAL, REVERSE};
struct Card { Suit suit; CardType type; };
Card deck[48];
int deckSize, remainingNormal, remainingReverse;

// ë§ ìœ„ì¹˜ & LED ë§µ
int horseCols[horseCount];
bool dotState[8][8];

// ì´ëª¨ì§€ ë§µ
static const char* suitNames[] = {"Diamond","Heart","Spade","Clover"};
static const char* suitIcons[] = {"â™¦","â™¥","â™ ","â™£"};

// í•¨ìˆ˜ ì›í˜•
void initPins(), initGame(), initDeck();
Card drawCard();
void advanceHorse(Card card), drawMatrix();
void printStatus(Card card), printWinner(int idx);

int reverseLedIndex = 1;
bool reverseMode = false;

void setup() {
  Serial.begin(9600);
  Serial.println("--- Poker Horse Race Initialized ---");
  randomSeed(analogRead(A0));

  pinMode(buttonPin, INPUT_PULLUP);
  pinMode(buzzerPin, OUTPUT);  // ë¶€ì € í•€ ì„¤ì •

  initPins();
  initGame();
  initDeck();
  shuffleDeck();
  printStatus({DIAMOND, NORMAL});
}

void loop() {
  drawMatrix();
  if (gameOver) return;

  bool curr = digitalRead(buttonPin);
  if (curr==LOW && prevButtonState==HIGH
      && millis()-lastDebounceTime>debounceDelay) {
    if (deckSize>0) {
      Card card = drawCard();
      advanceHorse(card);       // ë§ ì´ë™ + íš¨ê³¼ìŒ
      printStatus(card);
      // ìŠ¹ì íŒì •
      for (int i = 0; i < horseCount; i++) {
        if (horseCols[i] == trackLength) {
          printWinner(i);
          gameOver = true;
          break;
        }
      }
    } else {
      Serial.println("Deck empty. Game over.");
      gameOver = true;
    }
    lastDebounceTime = millis();
  }
  prevButtonState = curr;
}

// í•€ ì´ˆê¸°í™”
void initPins() {
  for (int i = 0; i < 8; i++) {
    pinMode(matrixSinkPins[i], OUTPUT);
    digitalWrite(matrixSinkPins[i], HIGH);
    pinMode(matrixSourcePins[i], OUTPUT);
    digitalWrite(matrixSourcePins[i], LOW);
  }
}

// ê²Œì„ ì´ˆê¸°í™”
void initGame() {
  memset(dotState, 0, sizeof(dotState));
  for (int i = 0; i < horseCount; i++) {
    horseCols[i] = 0;
    dotState[i*2][0] = true;
  }
  for (int c = 1; c <= 6; c++) dotState[7][c] = true;

  reverseLedIndex = 1; // ì´ˆê¸°í™”
}

// ë± ì´ˆê¸°í™” (48ì¥ + 6 ë¦¬ë²„ìŠ¤)
void initDeck() {
  Card temp[52]; int idx = 0;
  for (int s = 0; s < 4; s++)
    for (int k = 0; k < 13; k++)
      temp[idx++] = { (Suit)s, NORMAL };

  bool mark[52] = {};
  mark[0] = mark[13] = mark[26] = mark[39] = true;  // ë§ ì¹´ë“œ ì œì™¸
  int reserved = 0;
  while (reserved < 6) {
    int r = random(52);
    if (!mark[r]) { mark[r] = true; temp[r].type = REVERSE; reserved++; }
  }

  deckSize = remainingNormal = remainingReverse = 0;
  for (int i = 0; i < 52; i++) {
    if (!mark[i] || temp[i].type == REVERSE) {
      deck[deckSize++] = temp[i];
      if (temp[i].type == NORMAL) remainingNormal++;
      else remainingReverse++;
    }
  }
}

// ì¹´ë“œ ë½‘ê¸°
Card drawCard() {
  if (reverseMode && remainingReverse > 0) {
        for (int i = 0; i < deckSize; i++) {
      if (deck[i].type == REVERSE) {
        Card card = deck[i];
        deck[i] = deck[--deckSize];
        remainingReverse--;
        reverseMode = false;
        Serial.print("Draw REVERSE: "); Serial.println(suitNames[card.suit]);
        return card;
      }
    }
  } else if (!reverseMode && remainingNormal > 0) {
    for (int i = 0; i < deckSize; i++) {
      if (deck[i].type == NORMAL) {
        Card card = deck[i];
        deck[i] = deck[--deckSize];
        remainingNormal--;
        Serial.print("Draw NORMAL: "); Serial.println(suitNames[card.suit]);
        return card;
      }
    }
  }
  int r = random(deckSize);
  Card card = deck[r];
  deck[r] = deck[--deckSize];
  if (card.type == NORMAL) remainingNormal--;
  else remainingReverse--;
  Serial.print("Draw RANDOM: "); Serial.println(suitNames[card.suit]);
  return card;
}

// ë§ ì „ì§„/í›„ì§„ + íš¨ê³¼ìŒ
void advanceHorse(Card card) {
  int i = card.suit;
  dotState[i*2][horseCols[i]] = false;

  if (card.type == NORMAL) {
    if (horseCols[i] < trackLength) horseCols[i]++;
    tone(buzzerPin, 1000, 100);  // ì „ì§„ ì‚‘ ì†Œë¦¬

    // ì „ì§„ ì‹œ ìµœì†Œ ìœ„ì¹˜ ì°¾ê¸° (ë¦¬ë²„ìŠ¤ ì¹´ë“œ LED ë„ê¸°)
    int minc = 7;
    for (int j = 0; j < horseCount; j++) {
      if (horseCols[j] < minc) minc = horseCols[j];
    }
    if (minc >= 1 && minc <= 6) {
      // ê¼´ì§€ ë§ ìœ„ì¹˜ì™€ ë¦¬ë²„ìŠ¤ LED ìœ„ì¹˜ê°€ ê°™ê³  LEDê°€ ì¼œì ¸ ìˆì„ ë•Œë§Œ
      if (dotState[7][minc] && minc == horseCols[getLastHorseIndex()]) {
        dotState[7][minc] = false;
        reverseMode = true;
      } else {
    reverseMode = false;
  }
    }
  } else { // REVERSE ì¹´ë“œ: í›„ì§„
    // í›„ì§„í•  ë§ì´ íŠ¸ë™ ì•ˆì— ìˆìœ¼ë©´ í›„ì§„
    if (horseCols[i] > 0) horseCols[i]--;

    tone(buzzerPin, 500, 100);   // í›„ì§„ ë‚®ì€ í†¤

    // ë¦¬ë²„ìŠ¤ ì¹´ë“œ LED í•˜ë‚˜ êº¼ì£¼ê¸° (ì¢Œì¸¡ì—ì„œ ì˜¤ë¥¸ìª½ ìˆœì„œë¡œ)
    if (reverseLedIndex <= 6) {
      dotState[7][reverseLedIndex] = false;
      reverseLedIndex++;
    }
  }

  dotState[i*2][horseCols[i]] = true;
}

// ë§¤íŠ¸ë¦­ìŠ¤ ìŠ¤ìº”
void drawMatrix() {
  for (int r = 0; r < 8; r++) {
    digitalWrite(matrixSinkPins[r], LOW);
    for (int c = 0; c < 8; c++)
      digitalWrite(matrixSourcePins[c],
                   dotState[r][c] ? HIGH : LOW);
    delay(2);
    digitalWrite(matrixSinkPins[r], HIGH);
    for (int c = 0; c < 8; c++)
      digitalWrite(matrixSourcePins[c], LOW);
  }
}

// ìƒíƒœ ì¶œë ¥
void printStatus(Card card) {
  Serial.print("Drawn: ");
  Serial.print(suitIcons[card.suit]); Serial.print(" ");
  Serial.print(suitNames[card.suit]); Serial.print(" (");
  Serial.print(card.type==NORMAL?"Normal":"Reverse");
  Serial.println(")");

  Serial.print("Positions: ");
  for (int i = 0; i < horseCount; i++) {
    Serial.print(suitIcons[i]); Serial.print(":");
    Serial.print(horseCols[i]);
    if (i < horseCount-1) Serial.print(", ");
  }
  Serial.println();

  Serial.print("Remaining - ");
  Serial.print("ğŸ‚ ("); Serial.print(remainingNormal); Serial.print(") ");
  Serial.print("â†º("); Serial.print(remainingReverse); Serial.println(")");
  Serial.println("----------------------");
}

// ìŠ¹ì ì¶œë ¥ ë° ì¶•í•˜ ë©œë¡œë””
void printWinner(int idx) {
  Serial.print("Winner: ");
  Serial.print(suitIcons[idx]); Serial.print(" ");
  Serial.println(suitNames[idx]);
  Serial.println("=== Game Over ===");
  
  // ì¶•í•˜ ë©œë¡œë””: ë„(523Hz), ë¯¸(659Hz), ì†”(784Hz)
  tone(buzzerPin, 523, 200); delay(250);
  tone(buzzerPin, 659, 200); delay(250);
  tone(buzzerPin, 784, 400); delay(450);
  noTone(buzzerPin);
}

// ê¼´ì§€ ë§(ê°€ì¥ ìœ„ì¹˜ê°€ ì‘ì€ ë§)ì˜ ì¸ë±ìŠ¤ë¥¼ ë°˜í™˜
int getLastHorseIndex() {
  int lastIdx = 0;
  int minPos = horseCols[0];
  for (int i = 1; i < horseCount; i++) {
    if (horseCols[i] < minPos) {
      minPos = horseCols[i];
      lastIdx = i;
    }
  }
  return lastIdx;
}

void shuffleDeck() {
  for (int i = 0; i < deckSize; i++) {
    int r = random(i, deckSize);
    Card temp = deck[i];
    deck[i] = deck[r];
    deck[r] = temp;
  }
}
